ext {
    packageName = 'xx.xx.xx'

    minSdkVersion = 21
    compileSdkVersion = 30
    targetSdkVersion = 29
    buildToolsVersion = '30.0.2'

    // ----------- dependencies version start -------------
    versions = [:]
    versions.core = '1.0.0'
    versions.appcompat = '1.2.0'
    versions.core_ktx = '1.3.2'
    versions.constraintlayout = '2.0.4'
    versions.material = '1.2.1'
    versions.hilt = '2.31.2-alpha'
    versions.hilt_viewmodel = '1.0.0-alpha03'
    versions.lifecycle = '2.2.0'
    versions.room = '2.2.6'
    versions.coroutines = '1.3.5'
    versions.navigation = '2.3.3'
    versions.exo = '2.12.2'
    versions.timber = '4.7.1'
    versions.glide = '4.11.0'
    versions.easy_permission = '3.0.0'
    versions.su = '1.1.0.+'
    versions.retrofit = '2.9.0'
    versions.gson = '2.8.6'
    versions.smart_refresh_layout = '2.0.3'
    versions.flexbox = '2.0.1'
    versions.okhttp = '4.9.0'
    versions.camerax = '1.0.0-rc02'
    versions.asm = '9.1'
    versions.qrcode = '4.2.0'
    versions.blurry = '4.0.0'

    dpes = [:]
    dpes.core = "com.github.xionghaoo:core:$versions.core"
    dpes.appcompat = "androidx.appcompat:appcompat:$versions.appcompat"
    dpes.core_ktx = "androidx.core:core-ktx:$versions.core_ktx"
    dpes.material = "com.google.android.material:material:$versions.material"
    dpes.constraintlayout = "androidx.constraintlayout:constraintlayout:$versions.constraintlayout"
    dpes.lifecycle = [
        extensions: "androidx.lifecycle:lifecycle-extensions:$versions.lifecycle",
        livedata_ktx: "androidx.lifecycle:lifecycle-livedata-ktx:$versions.lifecycle",
        viewmodel_ktx: "androidx.lifecycle:lifecycle-viewmodel-ktx:$versions.lifecycle"
    ]
    dpes.hilt = [
        main: "com.google.dagger:hilt-android:$versions.hilt",
        android_compiler: "com.google.dagger:hilt-android-compiler:$versions.hilt",
        compiler: "androidx.hilt:hilt-compiler:$versions.hilt_viewmodel",
        lifecycle_viewmodel: "androidx.hilt:hilt-lifecycle-viewmodel:$versions.hilt_viewmodel"
    ]
    dpes.easy_permission = "pub.devrel:easypermissions:$versions.easy_permission"
    dpes.smart_refresh_layout = [
        kernel: "com.scwang.smart:refresh-layout-kernel:$versions.smart_refresh_layout",
        classics_header: "com.scwang.smart:refresh-header-classics:$versions.smart_refresh_layout"
    ]
    dpes.su = "eu.chainfire:libsuperuser:$versions.su"
    dpes.gson = "com.google.code.gson:gson:$versions.gson"
    dpes.retrofit = [
        main: "com.squareup.retrofit2:retrofit:$versions.retrofit",
        gson_converter: "com.squareup.retrofit2:converter-gson:$versions.retrofit"
    ]
    dpes.room = [
        runtime: "androidx.room:room-runtime:$versions.room",
        compiler: "androidx.room:room-compiler:$versions.room",
        ktx: "androidx.room:room-ktx:$versions.room"
    ]
    dpes.coroutines = "org.jetbrains.kotlinx:kotlinx-coroutines-android:$versions.coroutines"
    dpes.glide = [
        main: "com.github.bumptech.glide:glide:$versions.glide",
        compiler: "com.github.bumptech.glide:compiler:$versions.glide"
    ]
    dpes.flexbox = "com.google.android:flexbox:$versions.flexbox"
    dpes.okhttp = "com.squareup.okhttp3:okhttp:$versions.okhttp"
    dpes.camerax = [
        core: "androidx.camera:camera-core:$versions.camerax",
        camera2: "androidx.camera:camera-camera2:$versions.camerax",
        lifecycle: "androidx.camera:camera-lifecycle:$versions.camerax",
        view: "androidx.camera:camera-view:1.0.0-alpha21",
        extensions: "androidx.camera:camera-extensions:1.0.0-alpha21"
    ]
    dpes.asm = [
        "core": "org.ow2.asm:asm:$versions.asm",
        "commons": "org.ow2.asm:asm-commons:$versions.asm"
    ]
    // 二维码生成和扫描
    dpes.qrcode = "com.journeyapps:zxing-android-embedded:$versions.qrcode"
    // 毛玻璃背景
    dpes.blurry = "jp.wasabeef:blurry:$versions.blurry"
    // ----------- dependencies version end -------------

    // ----------- 项目环境配置 start -----------
    // 本地缓存数据库版本
    cache_db_version = '1'
    domain = [:]
    domain.uat = '\"ip.jsontest.com\"'
    domain.pre = '\"ip.jsontest.com\"'
    domain.prod = '\"ip.jsontest.com\"'

    tecentStorageHostUat = '\"https://cdn-vesta-dev.pgyscm.com\"'
    tecentStorageHost = '\"https://cdn-vesta-pro.pgyscm.com\"'
    // ----------- 项目环境配置 end -----------

    // ----------- 版本管理 start ---------------
    def today = new Date().format('yyMMdd').toString()
    // 计算版本名称最后一个字符
    versionLastCharacter = { String path ->
        if (path.contains('debug')) return
        def fileNames = fileTree(path).filter { it.name.contains('.apk') }.files.name
        def lastChars = []
        fileNames.forEach { name ->
            def matcher = name =~ /_[uat|pre]*([0-9]+[a-z])_/
            if (matcher.find()) {
                def fileDate = matcher.findAll().first()[1]
                if (fileDate.contains(today)) {
                    // 列出今天创建的文件
                    def dateMatcher = fileDate =~ /[\d]+([a-z])/
                    def lastChar = dateMatcher.findAll().first()[1]
                    lastChars << (int)(lastChar.charAt(0))
                }
            }
        }
        if (lastChars.size() > 0) {
            return (char)(lastChars.max() + 1)
        } else {
            return 'a'
        }
    }
    def lastUatReleaseCharacter = versionLastCharacter("./uat/release")
    def lastPreReleaseCharacter = versionLastCharacter("./pre/release")
    // 正式环境
    prodVersionCode = 1
    prodVersionName = "1.0.0"

    // 测试环境
    uatVersionCode = 2
    uatVersionName = "${prodVersionName}_uat${today}${lastUatReleaseCharacter}"

    // 预发布环境
    preVerisonCode = 1
    preVersionName = "${prodVersionName}_pre${today}${lastPreReleaseCharacter}"

    // 重命名输出的apk文件，对测试环境和预发布环境apk文件自动命名
    renameApk = {
        android.applicationVariants.all { variant ->
            variant.outputs.all { path ->
                // example: uatRelease_v1.0.0_20200101a_1.apk
                outputFileName = "${variant.name}_v${variant.versionName}_${variant.versionCode}.apk"
            }
        }
    }
    // ----------- 版本管理 end ---------------
}
